plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0' apply false
    id 'maven-publish'
    id 'signing'
}
group = 'pro.gravit.launcher'
version = '5.0.5-SNAPSHOT'

configure(subprojects.findAll { it.name != 'modules' }) {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'java'

    group = 'pro.gravit'

    repositories {
        mavenCentral()
        maven { url 'http://oss.sonatype.org/content/groups/public' }
        maven {
            url "http://clojars.org/repo/"
        }
    }

    configurations {
        apt
        aptCompileOnly
        aptOnly
        aptOnly.extendsFrom apt, aptCompileOnly
        compile.extendsFrom apt
        compileOnly.extendsFrom aptCompileOnly
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.annotationProcessorPath = configurations.aptOnly
    }
}

subprojects {
    tasks.withType(JavaCompile) {
        options.incremental = true // one flag, and things will get MUCH faster
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

defaultTasks 'build'

if (hasProperty('mavenUsername') && hasProperty('mavenPassword'))
publishing {
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            url = version.endsWith('SNAPSHOT') ? 'https://oss.sonatype.org/content/repositories/snapshots/' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username getProperty('mavenUsername')
                password getProperty('mavenPassword')
            }
        }
    }
}
